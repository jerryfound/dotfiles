#+TITLE: Emacs Configuration
#+PROPERTY: header-args:emacs-lisp :tangle ./.config/emacs/init.el :mkdirp yes

* 配置说明

这是一个基于 Org Mode 的 Emacs 配置，使用 Literate Programming 方式管理。主要功能：
- Org Mode 作为核心的笔记和任务管理系统
- 简洁高效的界面配置
- 模块化的包管理

保存此文件时会自动生成 =early-init.el= 和 =init.el= 配置文件。

* Early Init 配置
:PROPERTIES:
:header-args:emacs-lisp: :tangle ./.config/emacs/early-init.el
:END:

** XDG 目录规范

#+begin_src emacs-lisp
  ;; 定义 XDG 目录变量
  (defvar xdg-data-home (or (getenv "XDG_DATA_HOME")
        			  (expand-file-name "~/.local/share")))
  (defvar xdg-state-home (or (getenv "XDG_STATE_HOME")
        			   (expand-file-name "~/.local/state")))
#+end_src

** 包系统目录设置

#+begin_src emacs-lisp
  ;; 设置 elpa 目录位置
  (setq package-user-dir (expand-file-name "emacs/elpa" xdg-data-home))
  (make-directory package-user-dir t)
#+end_src

#+begin_src emacs-lisp
  ;; 设置 auto-save 目录位置
  (setq auto-save-list-file-prefix
        (expand-file-name "emacs/auto-save-list/.saves-" xdg-state-home))
  (make-directory (file-name-directory auto-save-list-file-prefix) t)
#+end_src

** 启动性能优化

#+begin_src emacs-lisp
(setq inhibit-startup-message t)  ; 禁用启动画面
(menu-bar-mode -1)        ; 禁用菜单栏
(tool-bar-mode -1)        ; 禁用工具栏
(scroll-bar-mode -1)      ; 禁用滚动条

(setq visible-bell t)     ; 可视化 Bell
#+end_src

* Init.el 配置

** 包管理系统

#+begin_src emacs-lisp
  ;; 使用 use-package 管理软件包
  (require 'package)
  (setq package-archives '(("gnu"    . "https://mirrors.tuna.tsinghua.edu.cn/elpa/gnu/")
                           ("nongnu" . "https://mirrors.tuna.tsinghua.edu.cn/elpa/nongnu/")
                           ("melpa"  . "https://mirrors.tuna.tsinghua.edu.cn/elpa/melpa/")))
  (package-initialize)

  (require 'use-package)

  (setq use-package-always-ensure t)    ; 自动安装缺少的插件
#+end_src

x** 系统基础配置

*** Custom 文件隔离

custom.el 存储的是:

- 通过 Customize 界面设置的选项值
- 主题、字体等用户偏好
- 标记为安全的 file-local variables

#+begin_src emacs-lisp
  (setq custom-file (expand-file-name "custom.el" user-emacs-directory))
  (when (file-exists-p custom-file)
    (load custom-file))
#+end_src

*** 备份系统

#+begin_src emacs-lisp
  ;; 设置备份文件位置
  (let ((backup-dir (expand-file-name "emacs/backups" xdg-state-home)))
    (setq backup-directory-alist (list (cons "." backup-dir)))
    (make-directory backup-dir t))

  ;; 保留最近5次备份
  (setq version-control t) ; 启用版本控制
  (setq kept-new-versions 5) ; 保留最近5个版本
  (setq kept-old-versions 0) ; 不保留旧版本
  (setq delete-old-versions t) ; 自动删除超出的版本
#+end_src

*** 书签系统

#+begin_src emacs-lisp
(setq bookmark-default-file (expand-file-name "emacs/bookmarks" xdg-data-home))
#+end_src

** 界面配置

*** UI 基础设置

#+begin_src emacs-lisp
(set-fringe-mode 10)      ; 边缘留白
(tooltip-mode -1)         ; 禁用提示框

;; 启动时最大化
(add-to-list 'default-frame-alist '(fullscreen . maximized))

(global-visual-line-mode 1)
#+end_src

*** 主题系统

#+begin_src emacs-lisp
(use-package ef-themes
  :ensure t
  :config
  ;; 主题轮换函数
  (defun ef-custom/cycle-themes ()
    "在 4 个 ef 主题中轮换"
    (interactive)
    (pcase (car custom-enabled-themes)
      ('ef-elea-light (load-theme 'ef-spring t))
      ('ef-spring (load-theme 'ef-bio t))
      ('ef-bio (load-theme 'ef-elea-dark t))
      (_ (load-theme 'ef-elea-light t)))
    (message "Switched to: %s" (car custom-enabled-themes)))
  
  ;; 快捷键
  (global-set-key (kbd "C-c t") 'ef-custom/cycle-themes)
  
  ;; 启动时加载
  (load-theme 'ef-elea-dark t))
#+end_src

*** 字体配置

#+begin_src emacs-lisp
(set-face-attribute 'default nil :font "Maple Mono NF CN-15")
#+end_src

** 编辑工具

*** Vertico 补全界面

#+begin_src emacs-lisp
(use-package vertico
  :ensure t
  :init
  (vertico-mode))
#+end_src

*** Consult 搜索与导航

Consult 提供了增强的搜索命令。这里同时配置 recentf 模式，为 =consult-recent-file= 提供数据支持。

#+begin_src emacs-lisp
(setq recentf-save-file (expand-file-name "emacs/recentf" xdg-state-home))
(recentf-mode 1)
(setq recentf-max-saved-items 50)

(use-package consult
  :ensure t
  :bind (("C-x b" . consult-buffer)
         ("C-x C-r" . consult-recent-file)))
#+end_src

** Org Mode

*** 基础配置

**** Org 包安装

#+begin_src emacs-lisp
(use-package org :ensure t)
#+end_src

**** 代码块编辑配置

#+begin_src emacs-lisp
(setq org-confirm-babel-evaluate nil)
(setq org-src-fontify-natively t)
(setq org-src-tab-acts-natively t)
(setq org-edit-src-content-indentation 0)
#+end_src

**** Babel 语言支持

#+begin_src emacs-lisp
(org-babel-do-load-languages
  'org-babel-load-languages
  '((emacs-lisp . t)))
#+end_src

**** Org 根目录

#+begin_src emacs-lisp
(setq org-directory "~/jerryfound.me/org.jerryfound.me/")
#+end_src

**** Org-tempo 模板

#+begin_src emacs-lisp
(with-eval-after-load 'org
  (require 'org-tempo)

  (add-to-list 'org-structure-template-alist '("sp" . "src python"))
  (add-to-list 'org-structure-template-alist '("se" . "src emacs-lisp")))
#+end_src

*** 编辑模式和阅读模式

org mode 本身是可见即可得模式, 比如说: 在文字两边加上 =**= 这个符号之后, 这个文字变成就变成了粗体, 并且两边都有星号.

但 org mode 的链接在写完 url 和 description 之后, 只会显示 description, 这样修改就比较麻烦了. 并且, org mode 本身的可见即可得模式在阅读情境下不够清爽.

所以我的打算是设置两种模式, 一种是编辑模式, 另一种是阅读模式. 阅读模式会隐藏所有的标记, 但仍要注意, 它不是一个 read-only 模式, 依然是可以编辑的.

#+begin_src emacs-lisp
(defun my/org-toggle-display-mode ()
  "切换 org-mode 的编辑模式和阅读模式"
  (interactive)
  (if org-hide-emphasis-markers
      ;; 切换到编辑模式
      (progn
        (setq-local org-hide-emphasis-markers nil)
        (setq-local org-link-descriptive nil)
        (setq-local org-pretty-entities nil)
        (org-indent-mode -1)
        (message "编辑模式"))
    ;; 切换到阅读模式
    (progn
      (setq-local org-hide-emphasis-markers t)
      (setq-local org-link-descriptive t)
      (setq-local org-pretty-entities t)
      (org-indent-mode 1)
      (message "阅读模式")))
  (font-lock-flush)
  (font-lock-ensure))

(with-eval-after-load 'org
  (define-key org-mode-map (kbd "C-c p") 'my/org-toggle-display-mode))
#+end_src

*** 视觉样式

**** Org 专用字体

#+begin_src emacs-lisp
(defun org-custom/font ()
  (setq buffer-face-mode-face '(:family "LXGW Bright Code" :height 180 :weight light))
  (buffer-face-mode)
  (setq-local line-spacing 0.25))

(add-hook 'org-mode-hook 'org-custom/font)
#+end_src

**** 标题层级样式

#+begin_src emacs-lisp
(custom-set-faces
 '(org-level-1 ((t (:height 1.3 :weight bold))))
 '(org-level-2 ((t (:height 1.2 :weight bold))))
 '(org-level-3 ((t (:height 1.1 :weight bold))))
 '(org-level-4 ((t (:height 1.0 :weight bold))))
 '(org-level-5 ((t (:height 1.0))))
 '(org-level-6 ((t (:height 1.0))))
 '(org-level-7 ((t (:height 1.0))))
 '(org-level-8 ((t (:height 1.0)))))
#+end_src

*** 任务管理

**** TODO 状态定义

在子弹笔记中, 一个笔记有三种类型: 任务, 事件和笔记.

对于任务, 初始状态是待办, 然后当天根据任务情况会跳转到其中一种状态: 完成, 迁移, 排期, 取消.

#+begin_src emacs-lisp
(setq org-todo-keywords
      '((sequence "待办(t)" "|" "完成(d)" "迁移(m)" "排期(s)" "取消(c)")
	(type "事件(e)" "|")
	(type "笔记(n)" "|")))

(setq org-todo-keyword-faces
    '(("待办" . (:foreground "#f38ba8" :weight bold))   ; 柔和红
      ("完成" . (:foreground "#a6e3a1" :weight bold))   ; 柔和绿
      ("迁移" . (:foreground "#fab387" :weight bold))   ; 柔和橙
      ("排期" . (:foreground "#f9e2af" :weight bold))   ; 柔和黄
      ("取消" . (:foreground "#6c7086" :weight bold))   ; 灰色
      ("事件" . (:foreground "#89b4fa" :weight bold))   ; 柔和蓝
      ("笔记" . (:foreground "#cba6f7" :weight bold)))) ; 柔和紫
#+end_src

**** 完成状态时间戳

在子弹笔记工作流中，只有"完成"状态表示任务真正结束，需要记录完成时间。而"迁移"、"排期"、"取消"虽然在 agenda 中不再显示（位于 | 后），但不算真正完成，不需要时间戳。

Org Mode 的 =org-log-done= 会为所有 | 后的状态添加 CLOSED，无法区分。因此这里通过钩子函数实现：只有切换到"完成"时添加 CLOSED 时间戳，切换到其他状态时移除。

#+begin_src emacs-lisp
;; 关闭全局的 log-done
(setq org-log-done nil)

;; 只为"完成"状态添加 CLOSED 时间戳
(defun my/org-add-closed-on-done ()
  "只有切换到'完成'状态时添加 CLOSED 时间戳，其他状态移除 CLOSED"
  (if (string= org-state "完成")
      (org-add-planning-info 'closed (org-current-effective-time))
    (org-add-planning-info nil nil 'closed)))

(add-hook 'org-after-todo-state-change-hook 'my/org-add-closed-on-done)
#+end_src

**** 完成状态增强

添加任务完成状态后的删除线,有些主题的完成状态视觉效果不明显。这个配置确保切换主题后删除线效果仍然保留。

#+begin_src emacs-lisp
;; 定义删除线应用函数
(defun my/org-done-strike-through ()
  "Add strike-through to DONE headlines"
  (setq org-fontify-done-headline t)
  (set-face-attribute 'org-headline-done nil
                      :strike-through t
                      :inherit 'org-done))

;; 初始加载时应用
(with-eval-after-load 'org
  (my/org-done-strike-through))

;; 主题切换后自动重新应用
(advice-add 'load-theme :after
            (lambda (&rest _)
              (my/org-done-strike-through)))
#+end_src

*** 快速捕获

**** Capture 模板配置

格式如下: ~* 待办 具体的待办事项~

#+begin_src emacs-lisp
  (setq org-default-notes-file (concat org-directory "/journal.org"))
   
  (setq org-capture-templates
        '(("t" "任务" entry (file+datetree "")
  	 "* 待办 %(format-time-string \"%H:%M\") %?")
  	("e" "事件" entry (file+datetree "")
  	 "* 事件 %(format-time-string \"%H:%M\") %?")
  	("n" "笔记" entry (file+datetree "")
  	 "* 笔记 %(format-time-string \"%H:%M\") %?")))

  (global-set-key (kbd "C-c c") 'org-capture)
#+end_src

** 工具函数

*** 配置重载

#+begin_src emacs-lisp
(global-set-key (kbd "C-c r") (lambda ()
				(interactive)
				(load-file "~/.config/emacs/init.el")
				(revert-buffer t t t)
				(message "init.el reload.")))
#+end_src

* 文档生成说明
:PROPERTIES:
:tangle: no
:END:

这是一个构建脚本。当保存此 Org 文件时，Emacs 会自动读取底部的 Local Variables，并触发 org-babel-tangle 来生成配置文件。

# Local Variables:
# eval: (add-hook 'after-save-hook (lambda () (let ((org-confirm-babel-evaluate nil)) (org-babel-tangle))) nil t)
# End:
